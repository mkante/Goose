plugins {
    id "com.gradle.plugin-publish" version "0.9.5"
}

apply plugin: 'groovy'
apply plugin: 'maven-publish'

def currentVers = '1.0-SNAPSHOT'
def descr = 'A database migration plugin for Gradle. This was inspired from Laravel artisan migration tool.'

sourceCompatibility = 1.6
version = currentVers
group = 'kante.goose'
archivesBaseName = 'goose'
description = descr

publishing {
    publications {
        app (MavenPublication) {
            groupId 'kante.goose'
            artifactId 'goose'
            version currentVers
            artifact jar
        }
    }
}

pluginBundle {
    website = 'https://github.com/mkante/Goose'
    vcsUrl = 'https://github.com/mkante/Goose'
    description = descr
    tags = [
            'migration', 'database',
            'mkante', 'mysql'
    ]

    plugins {
        goosePlugin {
            id = 'kante.goose.migration'
            displayName = 'Goose (Database migration)'
        }
    }
}

repositories {
    mavenCentral()
}

test {
    minHeapSize = "128m"
    useJUnit();
    testLogging.showStackTraces = true
    testLogging.showStandardStreams = true
    testLogging.showCauses = true
    testLogging.showExceptions = true
    outputs.upToDateWhen { false }
}

dependencies {
    compile 'org.slf4j:slf4j-api:1.7.20'
    compile localGroovy() //not needed for Java plugins
    compile 'ch.qos.logback:logback-classic:1.1.6'
    compile gradleApi()

    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile 'org.springframework:spring-context:4.2.5.RELEASE'
    testCompile 'org.springframework:spring-test:4.2.5.RELEASE'
    testCompile 'mysql:mysql-connector-java:5.1.38'
    testCompile 'org.xerial:sqlite-jdbc:3.8.11.2'
    testCompile 'org.springframework:spring-jdbc:4.2.4.RELEASE'
}

jar.doLast {

    File libDir = new File(buildDir, "libs");
    logger.info "Copying dependenices to: "+libDir.path;
    //FileCollection deps = sourceSets.main.runtimeClasspath ;
    FileCollection deps = configurations.compile;

    deps.each { dep ->
        copy{
            include '**/*.jar'
            from dep
            into libDir
        }
    }
}